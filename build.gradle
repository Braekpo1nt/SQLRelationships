plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'org.braekpo1nt'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

configurations {
    shade
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.7-R0.1-SNAPSHOT")
    
    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    implementation 'org.xerial:sqlite-jdbc:3.50.3.0'
    
    // https://mvnrepository.com/artifact/com.j256.ormlite/ormlite-jdbc
    implementation 'com.j256.ormlite:ormlite-jdbc:6.1'
    shade 'com.j256.ormlite:ormlite-jdbc:6.1'
}

tasks.shadowJar {
    // only dependencies labeled with the "shade" configuration will be included in the fat jar (alongside my own code)
    configurations = [project.configurations.shade]
    // doesn't add "-all" to the jar file name
    archiveClassifier = ""
    // from the inventoryFramework docs
    relocate 'com.j256.ormlite:ormlite-jdbc', 'org.braekpo1nt.ormlite-jdbc'
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
